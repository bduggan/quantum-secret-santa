#!/usr/bin/env perl6

use lib '.';
use emails '%emails';

my $groups = ( <comet cupid rudolph>, <dancer prancer>, <donner blitzen> );

my $mailer = 'msmtp';
my $sender = 'santa@north.pole';

sub send-santa-email($santa, $santa-email,  $santee) {
    my $prog = Proc::Async.new($mailer, $santa-email, :w)
        or die "error running $mailer";
    my $promise = $prog.start;
    await $prog.say: qq:to/END/;
        From: $sender
        To: { $santa.tc } <$santa-email>
        Subject: Your Secret Santee Is ____

        Hello { $santa.tc },

        You are the secret santa for (drum roll).....

                { $santee.tc }

        Don't lose this email, you are the only one
        who knows!

        Happy holidays,
        🎅

        p.s. this email was generated by $*PROGRAM-NAME at { DateTime.now }.

        END
    $prog.close-stdin;
}

sub generate-santas($groups) {
    my @santas = $groups.flat;
    my %pairs;
    repeat {
        %pairs = @santas Z=> @santas.permutations.pick;
    } until %pairs.none.kv.cache ⊆ $groups.any;
    return %pairs;
}

sub MAIN(Bool :$test) {
    my %gives-to = generate-santas($groups);
    for %gives-to.keys.sort -> $santa {
        my $santee = %gives-to{$santa};
        my $email = %emails{$santa} or die "no email address for $santa";
        if $test {
            say "$santa gives to $santee";
            next;
        }
        say "sending an email to $santa <$email>";
        send-santa-email($santa, $email, $santee);
    }
}
